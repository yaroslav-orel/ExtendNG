buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id 'org.sonarqube' version '2.6.2'
}

group = 'com.github.yaroslav-orel'
version = '1.1'
description 'Convenient TestNG Listeners'

ext {
    gradleScriptDir = "${rootProject.projectDir}/gradle"
    projectUrl = "https://github.com/yaroslav-orel/ExtendNG"
}


apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply from: "${gradleScriptDir}/bintray.gradle"
apply from: "${gradleScriptDir}/maven-publish.gradle"

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile 'org.testng:testng:6.14.3'
    compile 'com.google.guava:guava:26.0-jre'
    compile 'org.apache.commons:commons-lang3:3.8'
    compile 'com.github.wmacevoy:kiss:1.0'
    compile 'org.projectlombok:lombok:1.18.2'
    compile 'one.util:streamex:0.6.7'
    compile 'io.vavr:vavr:0.9.2'
    compile 'com.google.inject:guice:4.2.0:no_aop'

    testCompile 'org.mockito:mockito-core:2.21.0'
    testCompile 'org.assertj:assertj-core:3.11.1'
}

jacoco {
    toolVersion = '0.8.2'
}

sonarqube {
    properties {
        property "sonar.junit.reportPaths" , "${project.buildDir}/test-results/test"
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
    }
}

test{
    useTestNG()

    filter{
        includeTestsMatching 'unit.*'
        includeTestsMatching 'end.to.end.*'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


